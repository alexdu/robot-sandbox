.PROGRAM paletizare
    GLOBAL safe, st, pal, dx, dy, dz
    n = 12   ; numarul de piese
    ang = 60
    nl = 3
    nc = 2
    nn = 2
    
    PARAMETER HAND.TIME = 0.5
    SPEED 100 ALWAYS
    
    OPEN
    MOVE #safe
    BREAK
    
    FOR p = 1 TO n
        
        ; ordinea L-C-N
        
        i = INT((p-1) / (nc * nn))
        j = INT((p-1) / nn) MOD nc
        k = (p-1) MOD nn
        
        SET pick = SHIFT(st BY 0, 0, dz * (n - p))
        SET place = RZ(ang):TRANS(i*dx, j*dy, k*dz):RZ(-ang):pal
        
        TYPE p, i, j, k
        
        CALL pick.place(pick, place)
    END
    
    MOVE #safe
    BREAK
    
.END

.PROGRAM pick.place(pick,place)
    AUTO z.pick, z.place
    
    z.pick = 100
    z.place = 100
    
    OPEN
    APPRO pick, z.pick
    BREAK
    SPEED 30
    MOVES pick
    CLOSEI
    SPEED 30
    DEPARTS z.pick
    BREAK
    
    APPRO place, z.place
    BREAK
    SPEED 30
    MOVES place
    OPENI
    SPEED 30
    DEPARTS z.place
    BREAK
.END
