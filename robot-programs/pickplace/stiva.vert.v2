.PROGRAM stiva.vert()
    
    GLOBAL #safe, st.a, st.b  ; locaţii robot
    AUTO n, h, i, r
    AUTO pick, place
    
    n = 5;
    h = 15;
    
    SPEED 100 ALWAYS
    MOVET #safe, TRUE         ; mişcarea începe din #safe
    BREAK                     ; cu gripper-ul deschis
    
    FOR i = 1 TO n            ; r = nr. pieselor rămase în stiva st.a
        r = n - i + 1
        SET pick = SHIFT(st.a BY 0, 0, (r - 1) * h);
        SET place = SHIFT(st.b BY 0, 0, (i - 1) * h);
        
        CALL pick.place(pick, place)
    END
    
    MOVE #safe                ; întoarcere în \#safe
.END


.PROGRAM pick.place(pick, place)
    
    AUTO z.pick, z.place
    z.pick = 80
    z.place = 80
    
    PARAMETER HAND.TIME = 0.2
    OPEN
    
    APPRO pick, z.pick
    BREAK
    
    SPEED 50
    MOVES pick
    CLOSEI
    
    SPEED 30
    DEPARTS z.pick
    BREAK
    
    APPRO place, z.place
    BREAK
    
    SPEED 20
    MOVES place
    OPENI
    
    SPEED 50
    DEPARTS z.place
    BREAK
    
.END
