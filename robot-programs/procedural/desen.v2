.PROGRAM ia.pixul()
    PARAMETER HAND.TIME = 0.5
    TYPE "Iau pixul..."
    SPEED 30 ALWAYS
    OPEN
    APPRO loc.pix, 100
    MOVES loc.pix
    CLOSEI
    DEPARTS 100
    TYPE "Am luat pixul."
.END

.PROGRAM pune.pixul()
    TYPE "Pun pixul inapoi..."
    PARAMETER HAND.TIME = 0.3
    SPEED 30 ALWAYS
    APPRO loc.pix, 100
    MOVES loc.pix
    OPENI
    DEPARTS 100
    TYPE "Am pus pixul, ma retrag in #safe."
    MOVE #safe
.END


.PROGRAM vezi.foaia()
    TYPE "Stiu deja unde e foaia."
.END



.PROGRAM cerc()
    GLOBAL loc.foaie
    CALL vezi.foaia()
    IF DEFINED(loc.foaie) THEN
        CALL ia.pixul()
        CALL deseneaza.cerc()
        CALL pune.pixul()
    END
.END


.PROGRAM deseneaza.cerc()
    GLOBAL loc.foaie, p0
    AUTO centru, start, raza, t, pas.mm, pas.deg
    raza = 90
    pas.mm = 5
    pas.deg = (pas.mm / raza) * 180 / PI
    SET centru = SHIFT(p0 BY 90, 140, 0)
    SET start = SHIFT(centru BY raza, 0, 0)
    ; robotul va folosi setarea SPEED pentru segmentele lungi
    ; si DURATION pt. cele scurte (va alege varianta cea mai lenta)
    SPEED 100 ALWAYS
    DURATION pas.mm / 50 ALWAYS
    APPRO loc.foaie:start, 10
    MOVES loc.foaie:start
    BREAK
    FOR t = 0 TO 360 STEP pas.deg
        MOVES loc.foaie:SHIFT(centru BY raza*COS(t), raza*SIN(t), 0)
    END
    MOVES loc.foaie:start
    BREAK
    DEPARTS 10
.END


.PROGRAM picasso()
    GLOBAL loc.foaie
    CALL vezi.foaia()
    IF DEFINED(loc.foaie) THEN
        CALL ia.pixul()
        CALL deseneaza()
        CALL pune.pixul()
    END
.END

.PROGRAM deseneaza()
    AUTO lun, p, n, m, zup, x, y, i, j
    GLOBAL loc.foaie, p0
    zup = 5             ; înălţimea la care pixul se deplasează în aer
    ATTACH (lun, 4) "NFS"
    FOPENR (lun) "t.txt"
    
    TYPE "Desenez..."
    SPEED 100 ALWAYS
    APPROS loc.foaie:p0, 50
    BREAK
    
    READ (lun) n
    FOR i = 1 TO n
        READ (lun) m
        IF m > 1 THEN
            READ (lun) x, y
            SET p = loc.foaie:SHIFT(p0 BY x,y,0)
            SPEED 70
            APPROS p, zup
            MOVES p
            BREAK
            ; Nu am \cods{DURATION} $\Rightarrow$ se desenează cu viteză maximă
            FOR j = 2 TO m
                READ (lun) x, y
                SET p = loc.foaie:SHIFT(p0 BY x,y,0)
                MOVES p
            END
            BREAK
            DEPARTS zup
        END
    END
    FCLOSE (lun)
    DETACH (lun)
.END


